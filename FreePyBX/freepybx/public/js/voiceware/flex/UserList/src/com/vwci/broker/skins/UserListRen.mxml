<?xml version="1.0" encoding="utf-8"?>
<!-- 
This Source Code Form is subject to the terms of the Mozilla Public 
License, v. 2.0. If a copy of the MPL was not distributed with this 
file, You can obtain one at http://mozilla.org/MPL/2.0/.

Software distributed under the License is distributed on an "AS IS"
basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
License for the specific language governing rights and limitations
under the License.

The Original Code is PythonPBX/VoiceWARE.

The Initial Developer of the Original Code is Noel Morgan, 
Copyright (c) 2011-2012 VoiceWARE Communications, Inc. All Rights Reserved.

http://www.vwci.com/ 

You may not remove or alter the substance of any license notices (including
copyright notices, patent notices, disclaimers of warranty, or limitations 
of liability) contained within the Source Code Form of the Covered Software, 
except that You may alter any license notices to the extent required to 
remedy known factual inaccuracies.  
-->
<s:ItemRenderer name="CustomColumnItemRen"
				xmlns:fx="http://ns.adobe.com/mxml/2009"
				xmlns:s="library://ns.adobe.com/flex/spark"
				xmlns:mx="library://ns.adobe.com/flex/mx"
				autoDrawBackground="true"
			    doubleClickEnabled="true"
			    doubleClick="itemrenderer1_doubleClickHandler(event)"
				dataChange="dataChangeHandler(event);">
	
	<s:states>
		<s:State name="normal" />
		<s:State name="hovered" />
		<s:State name="selected" />
	</s:states>
	
	<s:layout>
		<s:HorizontalLayout gap="0"
						paddingLeft="3" paddingRight="3"
						paddingTop="3" paddingBottom="3"
						verticalAlign="middle" />
	</s:layout>
	
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.events.ItemClickEvent;
			import spark.events.IndexChangeEvent;			
			
			[Embed("assets/green_phone.png")]
			protected const GREEN:Class;
			
			[Embed("assets/red_phone.png")]
			protected const RED:Class;			
			
			[Embed("assets/red_error.png")]
			protected const UNREG:Class;					
			
			protected function dataChangeHandler(evt:Event):void {
				
				if (data.uuid) {
					bmpImg.source = RED;
				} else {
					bmpImg.source = GREEN;
				}
				
				if (data.is_online) {
					extension.text = "("+data.ext+")";	
					extension.setStyle("color", "navy");
				} else {
					bmpImg.source = UNREG;
					extension.text = "[Unregistered] ("+data.ext+")";	
					extension.setStyle("color", "red");
				}
				
				// Create custom context menu. 
				const callext_cmi:ContextMenuItem = new ContextMenuItem("Call Extension");
				callext_cmi.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, cmiext_menuItemSelect);
				
				const callmob_cmi:ContextMenuItem = new ContextMenuItem("Call Mobile");
				callmob_cmi.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, cmimob_menuItemSelect);				
				
				const cm:ContextMenu = new ContextMenu();
				cm.hideBuiltInItems();
				cm.customItems = [callext_cmi, callmob_cmi];		
				contextMenu = cm;				
			}
			
			protected function cmiext_menuItemSelect(evt:ContextMenuEvent):void {
				if (FlexGlobals.topLevelApplication.user_id != data.id && data.is_online) {
					FlexGlobals.topLevelApplication.callExtension(data.ext);
				}				
			}
			
			protected function cmimob_menuItemSelect(evt:ContextMenuEvent):void {
				FlexGlobals.topLevelApplication.callMobile(data.mobile);
			}			
			
			protected function itemrenderer1_doubleClickHandler(e:MouseEvent):void
			{
				ExternalInterface.call("outboundIMsg", data);
			}
			
		]]>
	</fx:Script>
	<s:BitmapImage id="bmpImg" width="16" height="16" />
	<s:Label id="labelDisplay" maxDisplayedLines="1" fontWeight="bold" showTruncationTip="true" width="100%" />
	<s:Label id="extension" textAlign="right" fontWeight="bold" />
	
	
</s:ItemRenderer>


